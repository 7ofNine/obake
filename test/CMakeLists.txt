# We have some multithreaded tests, so let's activate threading support.
include(YACMAThreadingSetup)

function(ADD_PIRANHA_TESTCASE arg1)
  add_executable(${arg1} ${arg1}.cpp)
  target_link_libraries(${arg1} piranha Threads::Threads)
  target_compile_options(${arg1} PRIVATE "$<$<CONFIG:DEBUG>:${PIRANHA_CXX_FLAGS_DEBUG}>" "$<$<CONFIG:RELEASE>:${PIRANHA_CXX_FLAGS_RELEASE}>")
  set_target_properties(${arg1} PROPERTIES CXX_VISIBILITY_PRESET hidden)
  set_target_properties(${arg1} PROPERTIES VISIBILITY_INLINES_HIDDEN TRUE)
  set_property(TARGET ${arg1} PROPERTY CXX_STANDARD 17)
  set_property(TARGET ${arg1} PROPERTY CXX_STANDARD_REQUIRED YES)
  set_property(TARGET ${arg1} PROPERTY CXX_EXTENSIONS NO)
  add_test(${arg1} ${arg1})
  # Add the path to the piranha DLL on windows, otherwise
  # running the tests will fail.
  if(WIN32)
    file(TO_NATIVE_PATH "${PROJECT_BINARY_DIR}" PIRANHA_TEST_NATIVE_BINARY_DIR)
    message(STATUS "Test native binary dir: ${PIRANHA_TEST_NATIVE_BINARY_DIR}")
    if(GENERATOR_IS_MULTI_CONFIG)
      message(STATUS "Barduffo: ${PIRANHA_TEST_NATIVE_BINARY_DIR}\\$<CONFIG>")
      set_tests_properties(${arg1} PROPERTIES ENVIRONMENT "PATH=${PIRANHA_TEST_NATIVE_BINARY_DIR}\\$<CONFIG>;$ENV{PATH}")
    else()
      set_tests_properties(${arg1} PROPERTIES ENVIRONMENT "PATH=${PIRANHA_TEST_NATIVE_BINARY_DIR};$ENV{PATH}")
    endif()
  endif()
endfunction()

ADD_PIRANHA_TESTCASE(math_is_zero)
ADD_PIRANHA_TESTCASE(math_pow)
ADD_PIRANHA_TESTCASE(math_safe_convert)
ADD_PIRANHA_TESTCASE(polynomials_polynomial)
ADD_PIRANHA_TESTCASE(series)
ADD_PIRANHA_TESTCASE(type_traits)
ADD_PIRANHA_TESTCASE(utils_demangle)

